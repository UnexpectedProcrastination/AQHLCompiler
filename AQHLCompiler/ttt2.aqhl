#load "IO.aqhl"

array local board[9] = {
		' ', ' ', ' ', 
		' ', ' ', ' ', 
		' ', ' ', ' '
};

array local indices[9] = {6, 7, 8, 3, 4, 5, 0, 1, 2};
array local aiMoves[9] = {4, 0, 2, 6, 8, 1, 3, 7, 5};

var local xWins = 0;
var local oWins = 0;

function Main() {
	var won = false;

	PrintBoard();
	PrintString("Your turn\n");
	
	while (true) {
		var space = ReadChar();
		
		if (!won && space #>= '1' && space #<= '9') {
			var index = indices[space - '1'];
			
			if (board[index] == ' ') {
				board[index] = 'X';
				
				if (CheckWinner('X')) {
					won = true;
					++xWins;
				
					Clear();
					PrintBoard();
					
					PrintString("You win!\nPress R to restart...\n");
					
					PrintFormat("X: %i O: %i", {xWins, oWins});
					
					[-2]; // Free {xWins, oWins}
				} else if (CheckDraw()) {
					Clear();
					PrintBoard();
				
					PrintString("Draw.\nPress R to restart...\n");
					
					PrintFormat("X: %i O: %i", {xWins, oWins});
					
					[-2]; // Free {xWins, oWins}
				} else {
					won = DoAITurn();
					
					Clear();
					PrintBoard();
				
					if (won) {
						++oWins;
						
						PrintString("You lose.\nPress R to restart...\n");
						
						PrintFormat("X: %i O: %i", {xWins, oWins});
						
						[-2]; // Free {xWins, oWins}
					} else {
						PrintString("Your turn\n");
					}
				}
			}
		} else if (space == 'R') {
			won = false;
		
			for (var i = 0; i < 9; i++) {
				board[i] = ' ';
			}
			
			Clear();
			PrintBoard();
			PrintString("Your turn\n");
		} else if (space == 'E') {
			break;
		}
	}
}

function local DoAITurn() {
	for (var i = 0; i #< 9; i += 3) {
		var rowTotal = board[i] == 'O';
		
		rowTotal += board[i + 1] == 'O';
		rowTotal += board[i + 2] == 'O';
		
		if (rowTotal == 2) {
			for (var j = i; j #< i + 3; j++) {
				if (board[j] == ' ') {
					board[j] = 'O';
					return true;
				}
			}
		}
	}
	
	for (var i = 0; i #< 3; i++) {
		var columnTotal = board[i] == 'O';
		
		columnTotal += board[i + 3] == 'O';
		columnTotal += board[i + 6] == 'O';
		
		if (columnTotal == 2) {
			for (var j = i; j #< 9; j += 3) {
				if (board[j] == ' ') {
					board[j] = 'O';
					return true;
				}
			}
		}
	}
	
	{
		var diagTotal = board[0] == 'O';
		diagTotal += board[4] == 'O';
		diagTotal += board[8] == 'O';
		
		if (diagTotal == 2) {
			for (var i = 0; i #< 9; i += 4) {
				if (board[i] == ' ') {
					board[i] = 'O';
					return true;
				}
			}
		}
	}
	
	{
		var diagTotal = board[2] == 'O';
		diagTotal += board[4] == 'O';
		diagTotal += board[6] == 'O';
		
		if (diagTotal == 2) {
			for (var i = 2; i #< 7; i += 2) {
				if (board[i] == ' ') {
					board[i] = 'O';
					return true;
				}
			}
		}
	}
	
	for (var i = 0; i #< 9; i += 3) {
		var rowTotal = board[i] == 'X';
		
		rowTotal += board[i + 1] == 'X';
		rowTotal += board[i + 2] == 'X';
		
		if (rowTotal == 2) {
			for (var j = i; j #< i + 3; j++) {
				if (board[j] == ' ') {
					board[j] = 'O';
					return false;
				}
			}
		}
	}
	
	for (var i = 0; i #< 3; i++) {
		var columnTotal = board[i] == 'X';
		
		columnTotal += board[i + 3] == 'X';
		columnTotal += board[i + 6] == 'X';
		
		if (columnTotal == 2) {
			for (var j = i; j #< 9; j += 3) {
				if (board[j] == ' ') {
					board[j] = 'O';
					return false;
				}
			}
		}
	}
	
	{
		var diagTotal = board[0] == 'X';
		diagTotal += board[4] == 'X';
		diagTotal += board[8] == 'X';
		
		if (diagTotal == 2) {
			for (var i = 0; i #< 9; i += 4) {
				if (board[i] == ' ') {
					board[i] = 'O';
					return false;
				}
			}
		}
	}
	
	{
		var diagTotal = board[2] == 'X';
		diagTotal += board[4] == 'X';
		diagTotal += board[6] == 'X';
		
		if (diagTotal == 2) {
			for (var i = 2; i #< 7; i += 2) {
				if (board[i] == ' ') {
					board[i] = 'O';
					return false;
				}
			}
		}
	}
	
	for (var i = 0; i #< 9; i++) {
		if (board[aiMoves[i]] == ' ') {
			board[aiMoves[i]] = 'O';
			return false;
		}
	}
	
	return false;
}

function local PrintBoard() {
	for (var i = 0; i #< 9;) {
		PrintChar(board[i++]);
		PrintChar('|');
		PrintChar(board[i++]);
		PrintChar('|');
		PrintChar(board[i++]);
		
		if (i != 9) {
			PrintString("\n-+-+-\n");
		} else {
			PrintChar('\n');
		}
	}
	
}

function local CheckWinner(player) {
	for (var i = 0; i #< 9; i += 3) {
		return # if board[i] == player && board[i] == board[i + 1] && board[i + 1] == board[i + 2];
	}
	
	for (var i = 0; i #< 3; i++) {
		return # if board[i] == player && board[i] == board[i + 3] && board[i + 3] == board[i + 6];
	}
	
	if (board[4] == player) {
		return # if (board[0] == board[4] && board[4] == board[8]) || (board[2] == board[4] && board[4] == board[6]);
	}
	
	return false;
}

function local CheckDraw() {
	for (var i = 0; i #< 9; i++) {
		return !# if board[i] == ' ';
	}
	
	return true;
}